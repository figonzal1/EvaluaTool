/*

 This file is subject to the terms and conditions defined in
 file 'LICENSE', which is part of this source code package

 Autor: Felipe González
 Email: felipe.gonzalezalarcon94@gmail.com

 Copyright (c) 2022

 Last modified 07-09-22 19:49
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'org.sonarqube'

android {
    def keystorePropertiesFile = rootProject.file("keystore.properties")
    // Initialize a new Properties() object called keystoreProperties.
    def keystoreProperties = new Properties()
    // Load your keystore.properties file into the keystoreProperties object.
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

    signingConfigs {
        evaluatoolsign {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    compileSdkVersion 33
    buildToolsVersion "33.0.2"
    defaultConfig {
        applicationId "cl.figonzal.evaluatool"
        minSdkVersion 23
        targetSdkVersion 33
        versionCode 34
        versionName "1.0.7"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            versionNameSuffix '-debug'
            applicationIdSuffix '.debug'
            debuggable true
            minifyEnabled false
            resValue "string", "app_name", "EvalúaTool-debug"
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.evaluatoolsign
            FirebasePerformance {
                instrumentationEnabled true
            }
            firebaseCrashlytics {
                mappingFileUploadEnabled true
            }
            resValue "string", "app_name", "EvaluaTool"
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }


    buildFeatures {
        viewBinding true
    }
    lint {
        checkDependencies true
    }
    flavorDimensions 'version'
    productFlavors {
        dev {
            dimension 'version'
            versionNameSuffix '-dev'
        }
        beta {
            dimension 'version'
            versionName '-beta'
        }
        prod {
            dimension 'version'
            signingConfig signingConfigs.evaluatoolsign
        }
    }
    namespace 'cl.figonzal.evaluatool'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.7.20'
    implementation "androidx.core:core-ktx:1.9.0"
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    implementation "com.google.android.material:material:1.8.0" //Material Design
    implementation 'androidx.preference:preference-ktx:1.2.0' //Preferencias de usuario
    implementation 'androidx.core:core-splashscreen:1.0.0' //splash screen

    //Life cycle components
    implementation("androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1")
    implementation("androidx.lifecycle:lifecycle-runtime-ktx:2.5.1")

    //FIREBASE
    implementation platform('com.google.firebase:firebase-bom:31.0.0')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-perf-ktx'

    implementation 'com.jakewharton.timber:timber:5.0.1' //Timber logs

    //Google Play
    implementation("com.google.android.gms:play-services-base:18.2.0")
    implementation("com.google.android.play:app-update-ktx:2.0.1")

    //KOIN
    implementation "io.insert-koin:koin-core:3.2.2"
    implementation "io.insert-koin:koin-android:3.3.0"

    //junit
    testImplementation("junit:junit:4.13.2")
    testImplementation("androidx.test:core-ktx:1.5.0")
    testImplementation("io.insert-koin:koin-test:3.2.0")
    testImplementation("io.insert-koin:koin-test-junit4:3.2.0")
    testImplementation("androidx.arch.core:core-testing:2.2.0")
    testImplementation("androidx.test.ext:truth:1.5.0")

    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation('androidx.test.espresso:espresso-contrib:3.5.1')

    //DEBUG
    debugImplementation("com.squareup.leakcanary:leakcanary-android:2.9.1")

    coreLibraryDesugaring("com.android.tools:desugar_jdk_libs:1.1.5")
}

sonarqube {
    properties {
        property("sonar.projectName", "EvaluaTool")
        property("sonar.projectKey", "EvaluaTool")
        property("sonar.test.inclusions", "**/*Test*/**")
        property("sonar.sourceEncoding", "UTF-8")
        property("sonar.sources", "src/main/java")
    }
}
